apply plugin: 'com.android.application'

def buildTime() {
    return new Date().format("yyMMddHHmmss", TimeZone.getTimeZone("GMT+8"))
}

def _versionCode() {
    return Integer.parseInt(new Date(getGitCommitTime()).format("yyMMddHHmm", TimeZone.getTimeZone("GMT+8")));
}

def getGitCommitHash() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Throwable th) {
        return "";
    }
}

def getGitCommitTime() {
    // maybe cause Exception
    return 'git show -s --format=%cd HEAD'.execute().text.trim()
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
        }
        release {
            keyAlias '律云在线'
            keyPassword 'lyun-app-release-psw'
            storeFile file('keystore/release.keystore')
            storePassword 'lyun-app-release-psw'
        }
    }

    defaultConfig {
        applicationId "com.lyun.lawyer"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode _versionCode()
        versionName "4.0.0"
        multiDexEnabled true
        signingConfig signingConfigs.debug
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "MIPUSH_APP_ID", '"2882303761517467816"'
        buildConfigField "String", "MIPUSH_APP_KEY", '"5931746772816"'
    }

    dataBinding {
        enabled true
    }

    sourceSets {
        main {
            res.srcDirs += 'src/main/res-nim'
        }
    }

    buildTypes {
        debug {
            debuggable true
            shrinkResources false
            signingConfig signingConfigs.debug

            versionNameSuffix = " debug-${buildTime()}"
            applicationIdSuffix = 'debug'

            buildConfigField "String", "HOST", '"http://172.16.133.175:8080"'
            buildConfigField "String", "MIPUSH_CERT_NAME", '"lawyerdebug"'
            buildConfigField "String", "BAIDU_MAP_AK", '"eqrUfj2oWMjy3pmQb8RRRjddu4Fc5zsF"'
            buildConfigField "int", "BAIDU_MAP_GEO_TABLE_ID", "138163"

            manifestPlaceholders.putAll([
                    APPLICATION_ID    : defaultConfig.applicationId + '.' + applicationIdSuffix,
                    NIM_APPKEY        : "2bbbb39221e2ea7d61904f61acc72d52",
                    UMENG_APP_KEY     : "55a4612067e58ebb5f004e14",
                    BaiduMobAd_STAT_ID: "ac161ce77e"])
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            //是否清理无用资源
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "HOST", '"https://lyt.lyun.com:8444"'
            buildConfigField "String", "MIPUSH_CERT_NAME", '"lawyer"'
            buildConfigField "String", "BAIDU_MAP_AK", '"I8mvwSd8WolPcyoTUCPf5QI1"'
            buildConfigField "int", "BAIDU_MAP_GEO_TABLE_ID", "108200"

            manifestPlaceholders.putAll([
                    APPLICATION_ID    : defaultConfig.applicationId,
                    NIM_APPKEY        : "2bbbb39221e2ea7d61904f61acc72d52",
                    MIPUSH_CERT_NAME  : "lawyer",
                    UMENG_APP_KEY     : "55a45f2967e58e1f1400529a",
                    BaiduMobAd_STAT_ID: "dc2fb90b27"])
        }

    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'android/databinding/DataBinderMapper.java'
        exclude 'android/databinding/DataBindingComponent.java'
        exclude 'android/databinding/DynamicUtil.java'
    }

    flavorDimensions "channel"

    productFlavors {
        official {
            dimension "channel"
        }
        baidu {
            dimension "channel"
        }
        apk91 {
            dimension "channel"
        }
        hiapk {
            dimension "channel"
        }
        wandoujia {
            dimension "channel"
        }
        tencent {
            dimension "channel"
        }
        xiaomi {
            dimension "channel"
        }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders.putAll([
                VERSION_CODE      : defaultConfig.versionCode,
                VERSION_NAME      : defaultConfig.versionName,
                BaiduMobAd_CHANNEL: name,
                UMENG_CHANNEL     : name])
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def fileName = "${variant.buildType.name}_Lawyer_v${defaultConfig.versionName}_${buildTime()}_${variant.productFlavors[0].name}.apk"
            if (variant.buildType.name == 'debug') {
                fileName = 'app-debug.apk'
            }
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                outputFileName = fileName
            }
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation project(':library')
    implementation project(':roundrectview')
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"
    testImplementation 'junit:junit:4.12'
    //testCompile 'org.mockito:mockito-core:1.10.19'
    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    annotationProcessor 'net.funol.databinding.watchdog:compiler:1.0.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    implementation 'pub.devrel:easypermissions:0.3.0'
}
